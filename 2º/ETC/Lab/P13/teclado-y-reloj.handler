### ESTRUCTURA DE COMPUTADORES (ETS Eng. Inf.)
##  
## marzo de 2014
##
## PRÁCTICA 13: SINCRONIZACIÓN POR INTERRUPCIONES
##
## Código inicial del manejador


##############################################################
##              ÁREA DE DATOS DEL MANEJADOR                 ##
##############################################################

			.kdata
var1:	.word 0xFFFFFFFF
contexto: .word 0 0 0 0

#############################################################
##                  CÓDIGO DEL MANEJADOR                   ##
#############################################################

          .ktext 0x80000080

    ## Preserva el contexto del programa de usuario ($t0, $a0, $v0, $at)
    .set noat
    sw $at, 0($k1)
    .set at

    sw $t0, 4($k1)
    sw $a0, 8($k1)
    sw $v0, 16($k1)

    ## Identifica y trata excepciones
    mfc0 $t0, $13
    move $v0, $t0
    andi $v0, $t0, 0x3C
    bne $v0, $zero, retexc      # Interrupciones tienen código = 0

    andi $t0, $t0, 0x1400
    li $a0, 0x0400
    beq $t0, $a0, int0
    li $a0, 0x1000
    beq $t0, $a0, int2
    j retexc

    ## Restaura el contexto
retexc:
    .set noat
    lw $at, 0($k1)
    .set at
    
    lw $t0, 4($k1)
    lw $a0, 8($k1)
    lw $v0, 16($k1)

    ## Restaura el modo usuario y vuelve al programa
    #$14 tiene la dirección de la instrucción (PC) donde se produce la excepción
    mfc0 $k0, $14       
	rfe                 
	jr $k0 

int0:
    li $v0, 11
    li $a0, '*'
    syscall

    li $t0, 0xffff0000
    lw $a0, 4($t0)
    j retexc

int2:
    la $t0, 0xffff0010
    li $a0, 0x01
    sb $a0, 0($t0)
    j retexc

#############################################################
##                    CÓDIGO DE INICIO                     ##
#############################################################

          .text
          .globl __start 
__start: 
    ## Código de inicio
    la $k1, contexto

    la $t0, 0xffff0000                  # Carga DB del teclado
    li $t1, 0x02                        
    sb $t1, 0($t0)                      # Activa las interrupciones en teclado


    la $t0, 0xffff0010                  # Carga DB del reloj
    li $t1, 0x01                        
    sb $t1, 0($t0)                      # Activa las interrupciones en reloj
    
    li $t1, 0x503                       # Desenmascara la interrupción de teclado y reloj
    mtc0 $t1, $12


    ## Salto al programa de usuario
	jal main

    ## Código de terminación
	li $v0, 10
	syscall	 # Función exit

